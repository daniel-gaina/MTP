load metaInterpreter.maude
load predef_modules.maude
load aux_modules.maude
load proof_tactics.maude
load grammar.maude
load file.maude

fmod COMMAND-PROCESSING is
 pr QUANTIFICATION-OPS .
 pr PARSING-RESULT .
 pr META-MTP-SIGN .
 pr META-LEVEL .
 pr LEXICAL .

 vars RIG RIG' NOM NOM' FLE FLE' SS : SortSet .
 vars M M' M'' TOP TOP' FLAT FLAT' : Module .
 vars VS VS' UV UV' EV EV' : VariableSet .
 vars T T' T'' T1 T2 T3 T4 TQ : Term .
 vars QIL QIL' ERR ERR' : QidList .
 vars PL PL' : ParameterDeclList .
 vars TL TL' ACC : TermList .
 var  SSDS : SubsortDeclSet .
 var  RP? : [ResultPair] .
 var  COND : Condition .
 var  PR : ParsingRes .
 var  ODS : OpDeclSet .
 var  TyL : TypeList .
 vars Ty Ty' : Type .
 var  AtS : AttrSet .
 var  DB : Database .
 vars S S' : String .
 var  Eq : Equation .
 vars Q Q' FQ : Qid .
 var  V : Variable .
 var  OP : OpDecl .
 var  I : Import .
 var  Srt : Sort .
 var  H : Header .
 var  N : Nat .


 op parseModule : Term -> ParsingRes .
 ceq parseModule('spec!_=_end[T,T']) = parseNoBubbles(M, T')
  if H := parseHeader(T) /\
     M := emptyModule(H) .
 ceq parseModule('spec*_=_end[T,T']) = parseNoBubbles(M, T')
  if H := parseHeader(T) /\
     M := emptyTheory(H) .

 op parseNoBubbles : Module Term -> ParsingRes .
 ceq parseNoBubbles(M, '__[T, T']) = [M'', (TL, TL'),
                                      < (RIG ; RIG'), (NOM ; NOM'), (FLE ; FLE') >, ERR ERR' ]
  if [M', TL, < RIG, NOM, FLE >, ERR] := parseDefNoBubble(M, T) /\
     [M'', TL', < RIG', NOM', FLE' >, ERR'] := parseNoBubbles(M', T') .
 eq parseNoBubbles(M, T) = parseDefNoBubble(M, T) .

 op parseDefNoBubble : Module Term -> ParsingRes .
 ***(
           PARSING IMPORTATION DECLARATIONS
 )
 ceq parseDefNoBubble(M, 'pr_.['token[T]]) = [M', empty, < none, none, none >, nil ]
  if Q := downQid(T) /\
     I := protecting Q . /\
     M' := addImports(M, I) .
 ceq parseDefNoBubble(M, 'ex_.['token[T]]) = [M', empty, < none, none, none >, nil ]
  if Q := downQid(T) /\
     I := extending Q . /\
     M' := addImports(M, I) .
 ceq parseDefNoBubble(M, 'inc_.['token[T]]) = [M', empty, < none, none, none >, nil ]
  if Q := downQid(T) /\
     I := including Q . /\
     M' := addImports(M, I) .
 ***(
           PARSING SORT DECLARATIONS
 )
 ceq parseDefNoBubble(M, 'rigid`sort_.['neTokenList[T]]) = [M', empty, < SS, none, none >, nil ]
  if SS := parseSortList(T) /\
     M' := addSorts(M, SS) .
 ceq parseDefNoBubble(M, 'rigid`sorts_.['neTokenList[T]]) = [M', empty, < SS, none, none >, nil ]
  if SS := parseSortList(T) /\
     M' := addSorts(M, SS) .
 ceq parseDefNoBubble(M, 'nominal`sort_.['neTokenList[T]]) = [M'', empty, < none, SS, none >, nil ]
  if SS := parseSortList(T) /\
     M' := addSorts(M, SS) /\
     SSDS := genSubsortNominal(SS) /\
     M'' := addSubsorts(M', SSDS) .
 ceq parseDefNoBubble(M, 'nominal`sorts_.['neTokenList[T]]) = [M'', empty, < none, SS, none >, nil ]
  if SS := parseSortList(T) /\
     M' := addSorts(M, SS) /\
     SSDS := genSubsortNominal(SS) /\
     M'' := addSubsorts(M', SSDS) .
 ceq parseDefNoBubble(M, 'sort_.['neTokenList[T]]) = [M', empty, < none, none, SS >, nil ]
  if SS := parseSortList(T) /\
     M' := addSorts(M, SS) .
 ceq parseDefNoBubble(M, 'sorts_.['neTokenList[T]]) = [M', empty, < none, none, SS >, nil ]
  if SS := parseSortList(T) /\
     M' := addSorts(M, SS) .
 ***(
           PARSING SUBSORT DECLARATIONS
 )
 ceq parseDefNoBubble(M, 'subsort_<_.['neTokenList[T],'token[T']]) = [M', empty, < none, none, none >, nil ]
  if SS := parseSortList(T) /\
     Ty := downQid(T') /\
     SSDS := createSubsortDecls(SS, Ty) /\
     M' := addSubsorts(M, SSDS) .
 ***(
          OPERATORS, EQUATIONS, AND VARIABLES ARE PARSED LATED.
          It makes sense because they might need sorts, kinds, and operators from imported modules
          and parameters.
 )
 eq parseDefNoBubble(M, T) = [M, T, < none, none, none >, nil ] [owise] . *** TODO!!!

 op parseArity : Term -> TypeList .
 eq parseArity('__[TL]) = parseArity*(TL) .
 eq parseArity(T) = downQid(T) [owise] .

 op parseArity* : TermList -> TypeList .
 eq parseArity*((T, TL)) = downQid(T) parseArity*(TL) .
 eq parseArity*(empty) = nil .

 op parseSortList : Term -> SortSet .
 eq parseSortList('__[TL]) = parseSortList*(TL) .
 eq parseSortList(T) = downQid(T) [owise] .

 op parseSortList* : TermList -> SortSet .
 eq parseSortList*((T, TL)) = downQid(T) ; parseSortList*(TL) .
 eq parseSortList*(empty) = none .

 op genSubsortNominal : SortSet -> SubsortDeclSet .
 eq genSubsortNominal(none) = none .
 eq genSubsortNominal(Srt ; SS) = subsort Srt < '@Nominal@ . genSubsortNominal(SS) .

 op parseAttr : Module Term -> AttrSet .
 eq parseAttr(M, '__[TL]) = parseAttr*(M, TL) .
 eq parseAttr(M, 'ctor.@Attr@) = ctor .
 eq parseAttr(M, 'iter.@Attr@) = iter .
 eq parseAttr(M, 'assoc.@Attr@) = assoc .
 eq parseAttr(M, 'comm.@Attr@) = comm .
 eq parseAttr(M, 'idem.@Attr@) = idem .
 eq parseAttr(M, 'memo.@Attr@) = memo .

 op parseAttr* : Module TermList -> AttrSet .
 eq parseAttr*(M, empty) = none .
 eq parseAttr*(M, (T, TL)) = parseAttr(M, T) parseAttr*(M, TL) .

 ***(
 ops id:_ : @Bubble@ -> @Attr@ .
 op left`id:_  : @Bubble@ -> @Attr@ .
 op right`id:_ : @Bubble@ -> @Attr@ .
 ops frozen`(_`) poly`(_`) strat`(_`) :
       @NeTokenList@ -> @AttrList@ .
 op prec_ : @Token@ -> @Attr@ .
 op gather`(_`) : @NeTokenList@ -> @Attr@ .
 ops format`(_`) : @NeTokenList@ -> @Attr@ .
 op metadata_ : @Token@ -> @Attr@ .
 )

***(
      Header
)
 op parseHeader : Term -> Header .
 ceq parseHeader('token[T]) = Q
  if Q := downQid(T) .
 ceq parseHeader('_`{_`}['token[T], T']) = Q{PL}
  if Q := downQid(T) /\
     PL := parsePL(T') .

 op parsePL : Term -> ParameterDeclList .
 ceq parsePL('_`,_[T, T']) = PL, PL'
  if PL := parsePL(T) /\
     PL' := parsePL(T') .
 ceq parsePL('_::_['token[T], 'token[T']]) = Q :: Q'
  if Q := downQid(T) /\
     Q' := downQid(T') .

 *** Top module, Flat module, Vars and equations
 op parseBubbles : Module Module TermList -> ParsingRes .
 eq parseBubbles(TOP, FLAT, TL) = parseBubbles(TOP, FLAT, none, TL, empty) .

 ***(
             PARSE VARIABLES
 )
 *** Top module, Flat module, VariableSet, Vars and equations, Acc
 op parseBubbles : Module Module VariableSet TermList TermList -> ParsingRes .
 eq parseBubbles(TOP, FLAT, VS, empty, ACC) = parseEqs(TOP, FLAT, VS, ACC) .
 ceq parseBubbles(TOP, FLAT, VS, ('var_:_.['neTokenList[T],'token[T']], TL), ACC) =
                             parseBubbles(TOP, FLAT, VS ; VS', TL, ACC)
  if VS' := parseVars(T, T') .
 ceq parseBubbles(TOP, FLAT, VS, ('vars_:_.['neTokenList[T],'token[T']], TL), ACC) =
                             parseBubbles(TOP, FLAT, VS ; VS', TL, ACC)
  if VS' := parseVars(T, T') .
 ***(
           PARSING OP DECLARATIONS
 )
 ceq parseBubbles(TOP, FLAT, VS, ('op_:`->_.['token[T],'token[T']], TL), ACC) = parseBubbles(TOP', FLAT', VS, TL, ACC)
  if Q := downQid(T) /\
     Q' := downQid(T') /\
     OP := op Q : nil -> Q' [none] . /\
     TOP' := addOps(TOP, OP) /\
     FLAT' := addOps(FLAT, OP) .
 ceq parseBubbles(TOP, FLAT, VS, ('op_:`->_`[_`].['token[T],'token[T'], T''], TL), ACC) =
                                                                                parseBubbles(TOP', FLAT', VS, TL, ACC)
  if Q := downQid(T) /\
     Q' := downQid(T') /\
     AtS := parseAttr(FLAT, T'') /\
     OP := op Q : nil -> Q' [AtS] . /\
     TOP' := addOps(TOP, OP) /\
     FLAT' := addOps(FLAT, OP) .
 ceq parseBubbles(TOP, FLAT, VS, ('op_:_->_.['token[T],'neTokenList[T'],'token[T'']], TL), ACC) =
                                                                                parseBubbles(TOP', FLAT', VS, TL, ACC)
  if Q := downQid(T) /\
     TyL := parseArity(T') /\
     Q' := downQid(T'') /\
     OP := op Q : TyL -> Q' [none] . /\
     TOP' := addOps(TOP, OP) /\
     FLAT' := addOps(FLAT, OP) .
 ceq parseBubbles(TOP, FLAT, VS, ('op_:_->_`[_`].['token[T1],'neTokenList[T2],'token[T3], T4], TL), ACC) =
                                                                                parseBubbles(TOP', FLAT', VS, TL, ACC)
  if Q := downQid(T1) /\
     TyL := parseArity(T2) /\
     Q' := downQid(T3) /\
     AtS := parseAttr(FLAT, T4) /\
     OP := op Q : TyL -> Q' [AtS] . /\
     TOP' := addOps(TOP, OP) /\
     FLAT' := addOps(FLAT, OP) .
 ***(
           PARSING PARTIAL OP DECLARATIONS
 )
 ceq parseBubbles(TOP, FLAT, VS, ('op_:`~>_.['token[T],'token[T']], TL), ACC) = parseBubbles(TOP', FLAT', VS, TL, ACC)
  if Q := downQid(T) /\
     Q' := downQid(T') /\
     OP := op Q : nil -> Q' [none] . /\
     TOP' := addOps(TOP, OP) /\
     FLAT' := addOps(FLAT, OP) .
 ceq parseBubbles(TOP, FLAT, VS, ('op_:`~>_`[_`].['token[T],'token[T'], T''], TL), ACC) =
                                                                      parseBubbles(TOP', FLAT', VS, TL, ACC)
  if Q := downQid(T) /\
     Q' := downQid(T') /\
     AtS := parseAttr(FLAT, T'') /\
     OP := op Q : nil -> Q' [AtS] . /\
     TOP' := addOps(TOP, OP) /\
     FLAT' := addOps(FLAT, OP) .
 ceq parseBubbles(TOP, FLAT, VS, ('op_:_~>_.['token[T],'neTokenList[T'],'token[T'']], TL), ACC) =
                                                                      parseBubbles(TOP', FLAT', VS, TL, ACC)
  if Q := downQid(T) /\
     TyL := parseArity(T') /\
     Q' := downQid(T'') /\
     OP := op Q : toKind(FLAT, TyL) -> toKind(FLAT, Q') [none] . /\
     TOP' := addOps(TOP, OP) /\
     FLAT' := addOps(FLAT, OP) .
 ceq parseBubbles(TOP, FLAT, VS, ('op_:_~>_`[_`].['token[T1],'neTokenList[T2],'token[T3], T4], TL), ACC) =
                                                                      parseBubbles(TOP', FLAT', VS, TL, ACC)
  if Q := downQid(T1) /\
     TyL := parseArity(T2) /\
     Q' := downQid(T3) /\
     AtS := parseAttr(FLAT, T4) /\
     OP := op Q : toKind(FLAT, TyL) -> toKind(FLAT, Q') [AtS] . /\
     TOP' := addOps(TOP, OP) /\
     FLAT' := addOps(FLAT, OP) .
 ***(
           PARSING FLEXIBLE OP DECLARATIONS
 )
 ceq parseBubbles(TOP, FLAT, VS, ('flexible`op_:`->_.['token[T],'token[T']], TL), ACC) = parseBubbles(TOP', FLAT', VS, TL, ACC)
  if Q := downQid(T) /\
     FQ := createFlexOpName(Q, nil) /\
     Q' := downQid(T') /\
     ODS := (op FQ : '@Nominal@ -> Q' [none] .
             op Q : nil -> Q' [none] .) /\
     TOP' := addOps(TOP, ODS) /\
     FLAT' := addOps(FLAT, ODS) .
 ceq parseBubbles(TOP, FLAT, VS, ('flexible`op_:`->_`[_`].['token[T],'token[T'], T''], TL), ACC) =
                                                                               parseBubbles(TOP', FLAT', VS, TL, ACC)
  if Q := downQid(T) /\
     FQ := createFlexOpName(Q, nil) /\
     Q' := downQid(T') /\
     AtS := parseAttr(FLAT, T'') /\
     ODS := (op FQ : '@Nominal@ -> Q' [AtS] .
             op Q : nil -> Q' [AtS] .) /\
     TOP' := addOps(TOP, ODS) /\
     FLAT' := addOps(FLAT, ODS) .
 ceq parseBubbles(TOP, FLAT, VS, ('flexible`op_:_->_.['token[T],'neTokenList[T'],'token[T'']], TL), ACC) =
                                                                               parseBubbles(TOP', FLAT', VS, TL, ACC)
  if TyL := parseArity(T') /\
     Q := downQid(T) /\
     FQ := createFlexOpName(Q, TyL) /\
     Q' := downQid(T'') /\
     ODS := (op FQ : '@Nominal@ TyL -> Q' [none] .
             op Q : TyL -> Q' [none] .) /\
     TOP' := addOps(TOP, ODS) /\
     FLAT' := addOps(FLAT, ODS) .
 ceq parseBubbles(TOP, FLAT, VS, ('flexible`op_:_->_`[_`].['token[T1],'neTokenList[T2],'token[T3], T4], TL), ACC) =
                                                                               parseBubbles(TOP', FLAT', VS, TL, ACC)
  if TyL := parseArity(T2) /\
     Q := downQid(T1) /\
     FQ := createFlexOpName(Q, TyL) /\
     Q' := downQid(T3) /\
     AtS := parseAttr(FLAT, T4) /\
     ODS := (op FQ : '@Nominal@ TyL -> Q' [AtS] .
             op Q : TyL -> Q' [AtS] .) /\
     TOP' := addOps(TOP, ODS) /\
     FLAT' := addOps(FLAT, ODS) .
 ***(
           PARSING PREDICATES
 )
 eq parseBubbles(TOP, FLAT, VS, ('pred_.[T], TL), ACC) =
              parseBubbles(TOP, FLAT, VS, ('op_:`->_.[T,'token[''Bool.Qid]], TL), ACC) .

 eq parseBubbles(TOP, FLAT, VS, ('pred_`[_`].['token[T],T''], TL), ACC) =
              parseBubbles(TOP, FLAT, VS, ('op_:`->_`[_`].['token[T],'token[''Bool.Qid], T''], TL), ACC) .
 eq parseBubbles(TOP, FLAT, VS, ('pred_:_.['token[T],'neTokenList[T']], TL), ACC) =
              parseBubbles(TOP, FLAT, VS, ('op_:_->_.['token[T],'neTokenList[T'],'token[''Bool.Qid]], TL), ACC) .
 eq parseBubbles(TOP, FLAT, VS, ('pred_:_`[_`].['token[T1],'neTokenList[T2],T4], TL), ACC) =
              parseBubbles(TOP, FLAT, VS, ('op_:_->_`[_`].['token[T1],'neTokenList[T2],'token[''Bool.Qid], T4], TL), ACC) .
 ***(
             OWISE
 )
 eq parseBubbles(TOP, FLAT, VS, (T, TL), ACC) = parseBubbles(TOP, FLAT, VS, TL, (ACC, T)) [owise] .

 ***(
             PARSE EQUATIONS
 )
 op parseEqs : Module Module VariableSet TermList -> ParsingRes .
 eq parseEqs(TOP, FLAT, VS, empty) = [TOP, VS, nil] .
 ceq parseEqs(TOP, FLAT, VS, (T, TL)) = parseEqs(M, FLAT, VS, TL)
  if Eq := parseEq(FLAT, VS, T) /\
     M := addEqs(TOP, Eq) .

 op parseEq : Module VariableSet Term -> Equation .
 ceq parseEq(M, VS, 'eq_=_.['bubble[T], 'bubble[T']]) = eq T1 = T2 [none] .
  if QIL := downQidList(T) /\
     QIL' := downQidList(T') /\
     {T1, Ty} := metaParse(M, VS, QIL, anyType) /\
     {T2, Ty'} := metaParse(M, VS, QIL', anyType) .
 op parseEq : Module VariableSet Term -> Equation .
 ceq parseEq(M, VS, 'eq_._=_.[TQ, 'bubble[T], 'bubble[T']]) = eq T1 = T2 [AtS] .
  if QIL := downQidList(T) /\
     QIL' := downQidList(T') /\
     {T1, Ty} := metaParse(M, VS, QIL, anyType) /\
     {T2, Ty'} := metaParse(M, VS, QIL', anyType) /\
     < UV, EV > := extractQuantVars(TQ, VS) /\
     AtS := generateQuantificationAtts(UV, EV) .
 ceq parseEq(M, VS, 'ceq_=_if_.['bubble[T], 'bubble[T'],T'']) = ceq T1 = T2 if COND [none] .
  if QIL := downQidList(T) /\
     QIL' := downQidList(T') /\
     {T1, Ty} := metaParse(M, VS, QIL, anyType) /\
     {T2, Ty'} := metaParse(M, VS, QIL', anyType) /\
     COND := parseCondition(M, T'', VS) .
 ceq parseEq(M, VS, 'ceq_._=_if_.[TQ, 'bubble[T], 'bubble[T'],T'']) = ceq T1 = T2 if COND [AtS] .
  if QIL := downQidList(T) /\
     QIL' := downQidList(T') /\
     {T1, Ty} := metaParse(M, VS, QIL, anyType) /\
     {T2, Ty'} := metaParse(M, VS, QIL', anyType) /\
     COND := parseCondition(M, T'', VS) /\
     < UV, EV > := extractQuantVars(TQ, VS) /\
     AtS := generateQuantificationAtts(UV, EV) .

 op parseCondition : Module Term VariableSet -> Condition .
 eq parseCondition(M, '_/\_[TL], VS) = parseCondition*(M, TL, VS) .
 ceq parseCondition(M, '_=_['bubble[T], 'bubble[T']], VS) = (T1 = T2)
  if QIL := downQidList(T) /\
     QIL' := downQidList(T') /\
     {T1, Ty} := metaParse(M, VS, QIL, anyType) /\
     {T2, Ty'} := metaParse(M, VS, QIL', anyType) .

 op parseCondition* : Module TermList VariableSet -> Condition .
 eq parseCondition*(M, empty, VS) = nil .
 eq parseCondition*(M, (T, TL), VS) = parseCondition(M, T, VS) /\ parseCondition*(M, TL, VS) .

 op parseVars : Term Term -> VariableSet .
 ceq parseVars(T, T') = createVars(QIL, Ty)
  if QIL := parseVars(T) /\
     Ty := downQid(T') .

 op parseVars : Term -> QidList .
 eq parseVars('__[TL]) = parseVars*(TL) .
 eq parseVars(T) = downQid(T) [owise] .

 op parseVars* : TermList -> QidList .
 eq parseVars*(empty) = nil .
 eq parseVars*((T, TL)) = downQid(T) parseVars*(TL) .

 op createVars : QidList Type -> VariableSet .
 eq createVars(nil, Ty) = none .
 ceq createVars(Q QIL, Ty) = V ; createVars(QIL, Ty)
  if V := qid(string(Q) + ":" + string(Ty)) .

 op extractModules : Term -> TermList .
 eq extractModules('__[TL]) = TL .
 eq extractModules(T) = T [owise] .

 op createSubsortDecls : SortSet Type -> SubsortDeclSet .
 eq createSubsortDecls(none, Ty) = none .
 eq createSubsortDecls(Ty ; SS, Ty') = subsort Ty < Ty' .
                                       createSubsortDecls(SS, Ty') .

 op createFlexOpName : Qid TypeList -> Qid .
 eq createFlexOpName(Q, nil) = qid("@_" + string(Q)) .
 eq createFlexOpName(Q, TyL) = $createFlexOpName(Q, | TyL |) [owise] .

 op $createFlexOpName : Qid NzNat -> Qid .
 ceq $createFlexOpName(Q, N) = qid("@_" + string(Q))
  if S := string(Q) /\
     find(S, "_", 0) =/= notFound .
 ceq $createFlexOpName(Q, N) = Q'
  if S := string(Q) /\
     find(S, "_", 0) == notFound /\
     Q' := qid("@_" + string(Q) + addUnderscores(N)) .

 op |_| : TypeList -> Nat .
 eq | nil | = 0 .
 eq | Ty TyL | = s(| TyL |) .

 op addUnderscores : NzNat -> String .
 eq addUnderscores(N) = "(" + $addUnderscores(N) + ")" .

 op $addUnderscores : NzNat -> String .
 eq $addUnderscores(1) = "_" .
 eq $addUnderscores(s(N)) = "_," + $addUnderscores(N) [owise] .

 op vars2str : VariableSet -> String .
 eq vars2str(none) = "" .
 eq vars2str(V) = string(V) .
 eq vars2str(V ; VS) = string(V) + " ; " + vars2str(VS) [owise] .

 op generateQuantificationAtts : VariableSet VariableSet -> AttrSet .
 eq generateQuantificationAtts(UV, none) = none .
 eq generateQuantificationAtts(UV, EV) = nonexec metadata(vars2str(EV)) .
endfm

mod MTP is
 pr (MAYBE * (op maybe to noMod)){Module} .
 pr (MAYBE * (op maybe to noQid)){Qid} .
 pr (MAYBE * (op maybe to null)){Oid} .
 pr COMMAND-PROCESSING .
 pr META-MTP-COMM-SIGN .
 inc META-INTERPRETER .
 pr PRELOAD-LIST .
 inc STD-STREAM .
 pr DATABASE .
 pr TACTICS .
 inc FILE .

 vars Text Original Read Source STR MSG : String .
 vars TL BUBBS MODL IN : TermList .
 vars T T' T'' T1 T2 T3 T4 : Term .
 vars FHIn FHOut MI : Oid .
 var  MM : Maybe{Module} .
 var  AtS : AttributeSet .
 var  LP : List{Preload} .
 vars DB DB' : Database .
 vars QIL ERR : QidList .
 vars M M' TOP : Module .
 var  VS : VariableSet .
 var  MQ : Maybe{Qid} .
 var  SI : SortInfo .
 vars Ty Ty' : Type .
 var  GL : GoalList .
 var  G : Goal .
 var  Q : Qid .
 var  N : Nat .

 sort State .
 *** Initial state, metainterpreter is created
 op init : -> State [ctor] .
 *** Loading from standard database
 op load-std-db : -> State [ctor] .
 *** Loading grammar
 op load-grammar : -> State [ctor] .
 *** Loading command grammar
 op load-comm-grammar : -> State [ctor] .
 *** Create user metainterpreter
 op load-user-mi : -> State [ctor] .
 *** Load modules into user metainterpreter
 op load-user-mods : -> State [ctor] .
 *** Waiting input from the user
 op idle : -> State [ctor] .
 *** Reading file
 op readFile : -> State [ctor] .
 *** Parsing Module - metaparse
 op parseModMetaparse : -> State [ctor] .
 *** Parsing - create module list
 op parseList : -> State [ctor] .
 *** Parsing Module
 op parseModNoBubbles : -> State [ctor] .
 *** Parsing Module
 op parseModBubbles : -> State [ctor] .
 *** Parsing Module
 op parseModule : -> State [ctor] .
 *** Get parsed module
 op getModule : -> State [ctor] .
 *** Parsing command
 op parseComm : -> State [ctor] .
 *** Execute command
 op executeComm : -> State [ctor] .
 *** Waits for a wrote msg and returns to idle with getLine
 op print&idle : -> State [ctor] .
 *** Waits for a wrote msg and tries to parse the next module
 op print&noBubbles : -> State [ctor] .
 *** Extracts the goal module from the metainterpreter
 op processGoal : -> State [ctor] .



 op foo : -> State [ctor] .

 *** Object and class definition
 op MTP : -> Cid .
 op mtp : -> Oid .

 *** Attributes
 op uload:_ : List{Preload} -> Attribute [ctor gather (&)] .
 op load:_ : List{Preload} -> Attribute [ctor gather (&)] .
 op parsModName:_ : Maybe{Qid} -> Attribute [ctor] .
 op parsedModules:_ : TermList -> Attribute [ctor] .
 op module:_ : Maybe{Module} -> Attribute [ctor] .
 op current:_ : Maybe{Qid} -> Attribute [ctor] .
 op top:_ : Maybe{Module} -> Attribute [ctor] .
 op bubbles:_ : TermList -> Attribute [ctor] .
 op file:_ : Maybe{Oid} -> Attribute [ctor] .
 op input:_ : TermList -> Attribute [ctor] .
 op goal:_ : GoalList -> Attribute [ctor] .
 op db:_ : Database -> Attribute [ctor] .
 op state:_ : State -> Attribute [ctor] .
 op out:_ : String -> Attribute [ctor] .
 op umi:_ : Oid -> Attribute [ctor] .
 op mi:_ : Oid -> Attribute [ctor] .

 *** Initial value for loading into the parsing metainterpreter
 op modList : -> List{Preload} .
 eq modList = module('TRUTH-VALUE) module('BOOL-OPS) module('TRUTH) module('BOOL)
              module('EXT-BOOL) module('NAT) module('INT) module('RAT) module('FLOAT)
              module('STRING) module('CONVERSION) module('QID) module('TRIV)
              view('Qid) module('LIST) module('QID-LIST) module('MTP-SIGN)
              module('COMMAND-SIGN) .

 *** Initial value for loading into the user metainterpreter
 op umodList : -> List{Preload} .
 eq umodList = module('TRUTH-VALUE) module('BOOL-OPS) module('TRUTH) module('BOOL)
               module('NAT) module('TRIV) module('@NOMINAL@) .

 *** Run command
 op run : -> Configuration [ctor] .

 ***                                 ***
 ***          PARSE COMMAND          ***
 ***                                 ***

 rl [parseComm] : gotLine(mtp, stdin, Text)
    < mtp : MTP | mi: MI, state: idle, AtS >
 => < mtp : MTP | mi: MI, state: parseComm, AtS >
    parseTerm(MI, mtp, 'COMM-GRAMMAR, none, tokenize(Text), '@Command@) .

 rl [parseComm] :
    parsedTerm(mtp, MI, {T, Ty})
    < mtp : MTP | mi: MI, state: parseComm, input: IN, AtS >
 => < mtp : MTP | mi: MI, state: executeComm, input: T, AtS > .

 ***                        ***
 ***          LOAD          ***
 ***                        ***

 crl [loadComm] :
    < mtp : MTP | state: executeComm, input: ('load_['token[T]]), AtS >
 => < mtp : MTP | state: readFile, input: empty, AtS >
    openFile(fileManager, mtp, STR, "r")
  if STR := string(downQid(T)) .

 ***                          ***
 ***          REDUCE          ***
 ***                          ***

 crl [redComm] :
     < mtp : MTP | state: executeComm, input: ('red_['bubble[T]]), module: M, AtS >
  => < mtp : MTP | state: print&idle, input: empty, module: M, AtS >
     write(stdout, mtp, STR)
  if {T', Ty} := metaParse(M, downQidList(T), anyType) /\
     {T'', Ty'} := metaReduce(M, T') /\
     QIL := metaPrettyPrint(M, T'') /\
     STR := "\nResult: " + printTokens(QIL) + "\n" .

 ***                            ***
 ***          OPENFILE          ***
 ***                            ***

 rl [openedFile] : openedFile(mtp, fileManager, FHIn)
    < mtp : MTP | file: null, state: readFile, AtS >
 => < mtp : MTP | file: FHIn, state: readFile, AtS >
    getLine(FHIn, mtp) .

 rl [readingModule] : gotLine(mtp, FHIn, Text)
    < mtp : MTP | file: FHIn, out: Read, state: readFile, AtS >
 => if Text == ""
    then < mtp : MTP | file: FHIn, out: Read, state: readFile, AtS >
         closeFile(FHIn, mtp)
    else < mtp : MTP | file: FHIn, out: (Read + Text), state: readFile, AtS >
         getLine(FHIn, mtp)
    fi .

 ***                                ***
 ***          PARSE MODULE          ***
 ***                                ***

 rl [parseModuleMetaparse] :
    < mtp : MTP | out: Read, state: parseModMetaparse, mi: MI, AtS >
 => < mtp : MTP | out: "", state: parseList, mi: MI, AtS >
    parseTerm(MI, mtp, 'GRAMMAR, none, tokenize(Read), '@SpecList@) .

 crl [parseModuleNoBubblesOK] :
     parsedTerm(mtp, MI, {T, Ty})
     < mtp : MTP | state: parseList, mi: MI, parsedModules: MODL, AtS >
  => < mtp : MTP | state: parseModNoBubbles, mi: MI, parsedModules: TL, AtS >
  if TL := extractModules(T) .

 crl [parseNextModule] :
     < mtp : MTP | db: DB, state: parseModNoBubbles, umi: MI,
                   parsModName: MQ, bubbles: BUBBS, top: MM,
                   parsedModules: (T, MODL), AtS >
  => < mtp : MTP | db: DB', state: parseModBubbles, umi: MI,
                   parsModName: getName(M), bubbles: TL, top: M,
                   parsedModules: MODL, AtS >
     insertModule(MI, mtp, M)
  if [M, TL, SI, ERR] := parseModule(T) /\
     DB' := DB[getName(M) |-> SI] .

 rl [noMoreModules] :
    < mtp : MTP | state: parseModNoBubbles, parsedModules: empty, AtS >
 => < mtp : MTP | state: idle, parsedModules: empty, AtS >
    getLine(stdin, mtp, "MTP> ") .

 rl [parseModule_GetFlatModule] :
    insertedModule(mtp, MI)
    < mtp : MTP | state: parseModBubbles, umi: MI, parsModName: Q, AtS >
 => < mtp : MTP | state: parseModBubbles, umi: MI, parsModName: Q, AtS >
    showModule(MI, mtp, Q, true) .

 crl [parseBubbles] :
     showingModule(mtp, MI, M)
     < mtp : MTP | state: parseModBubbles, umi: MI, bubbles: BUBBS,
                   top: TOP, parsModName: Q, current: MQ, db: DB, AtS >
  => < mtp : MTP | state: parseModule, umi: MI, bubbles: empty,
                   top: noMod, parsModName: noQid, current: Q, db: (DB[Q |-> VS]), AtS >
     insertModule(MI, mtp, M')
  if [M', VS, QIL] := parseBubbles(TOP, M, BUBBS) [print M' "\ncurrent " Q] .

 rl [parsingComplete] :
    insertedModule(mtp, MI)
    < mtp : MTP | state: parseModule, umi: MI, current: Q, AtS >
 => < mtp : MTP | state: getModule, umi: MI, current: Q, AtS >
    showModule(MI, mtp, Q, true).

 crl [updateNewModule] :
     showingModule(mtp, MI, M)
     < mtp : MTP | state: getModule, umi: MI, current: Q, module: MM, AtS >
  => < mtp : MTP | state: print&noBubbles, umi: MI, current: Q, module: M, AtS >
     write(stdout, mtp, MSG)
  if MSG := "\nModule " + string(Q) + " introduced.\n\n" .


 *** TODO: parse error

 rl [print&noBubbles] : wrote(mtp, stdout)
    < mtp : MTP | state: print&noBubbles, AtS >
 => < mtp : MTP | state: parseModNoBubbles, AtS > .

 rl [print&idle] : wrote(mtp, stdout)
    < mtp : MTP | state: print&idle, AtS >
 => < mtp : MTP | state: idle, AtS >
    getLine(stdin, mtp, "MTP> ") .

 ***                               ***
 ***          CLOSE FILE           ***
 ***                               ***

 rl [closedFile] : closedFile(mtp, FHIn)
    < mtp : MTP | file: FHIn, state: readFile, AtS >
 => < mtp : MTP | file: null, state: parseModMetaparse, AtS > .

 ***                         ***
 ***          GOAL           ***
 ***                         ***

 crl [moduleGoal] :
     < mtp : MTP | state: executeComm, input: ('goal_|-`eq_._=_['token[T1],T2,T3,T4]),
                   umi: MI, AtS >
  => < mtp : MTP | state: processGoal, input: ('goal_|-`eq_._=_['token[T1],T2,T3,T4]),
                   umi: MI, AtS >
     showModule(MI, mtp, Q, true)
  if Q := downQid(T1) .

 crl [processGoal] :
     showingModule(mtp, MI, M)
     < mtp : MTP | state: processGoal, input: ('goal_|-`eq_._=_['token[T1],T2,T3,T4]),
                   umi: MI, goal: GL, db: DB, AtS >
  => < mtp : MTP | state: print&idle, input: empty, umi: MI, goal: G, db: DB, AtS >
     write(stdout, mtp, "\nGoal introduced.\n")
  if Q := downQid(T1) /\
     G := processGoal(M, T2, T3, T4, getVS(DB[Q])) .

 ***                         ***
 ***          EXIT           ***
 ***                         ***

 rl [exit] :
    < mtp : MTP | state: executeComm, input: ('exit.@Command@), AtS >
 => write(stdout, mtp, "Thanks for using MTP!\n") [print AtS] .

 rl [quit] :
    < mtp : MTP | state: executeComm, input: ('q.@Command@), AtS >
 => write(stdout, mtp, "Thanks for using MTP!\n") .

 ***                        ***
 ***          ERROR         ***
 ***                        ***

 rl [parseCommError] :
    parsedTerm(mtp, MI, noParse(N))
    < mtp : MTP | mi: MI, state: parseComm, AtS >
 => < mtp : MTP | mi: MI, state: print&idle, AtS >
    write(stdout, mtp, "The introduced command does not exit.\n") .

 ***                          ***
 ***          CREATE          ***
 ***      METAINTERPRETER     ***
 ***                          ***

 rl [init-module] :
    createdInterpreter(mtp, interpreterManager, MI)
    < mtp : MTP | state: init, load: module(Q) LP, AtS >
 => < mtp : MTP | state: load-std-db, mi: MI, load: LP, AtS >
    insertModule(MI, mtp, upModule(Q, false)) .

 rl [init-view] :
    createdInterpreter(mtp, interpreterManager, MI)
    < mtp : MTP | state: init, load: view(Q) LP, AtS >
 => < mtp : MTP | state: load-std-db, mi: MI, load: LP, AtS >
    insertView(MI, mtp, upView(Q)) .

 rl [load-predef-module-from-module] :
    insertedModule(mtp, MI)
    < mtp : MTP | state: load-std-db, load: module(Q) LP, AtS >
 => < mtp : MTP | state: load-std-db, load: LP, AtS >
    insertModule(MI, mtp, upModule(Q, false)) .

 rl [load-predef-view-from-module] :
    insertedModule(mtp, MI)
    < mtp : MTP | state: load-std-db, load: view(Q) LP, AtS >
 => < mtp : MTP | state: load-std-db, load: LP, AtS >
    insertView(MI, mtp, upView(Q)) .

 rl [load-predef-module-from-view] :
    insertedView(mtp, MI)
    < mtp : MTP | state: load-std-db, load: module(Q) LP, AtS >
 => < mtp : MTP | state: load-std-db, load: LP, AtS >
    insertModule(MI, mtp, upModule(Q, false)) .

 rl [load-predef-view-from-view] :
    insertedView(mtp, MI)
    < mtp : MTP | state: load-std-db, load: view(Q) LP, AtS >
 => < mtp : MTP | state: load-std-db, load: LP, AtS >
    insertView(MI, mtp, upView(Q)) .

 rl [load-predef-finished] :
    insertedModule(mtp, MI)
    < mtp : MTP | state: load-std-db, load: nil, AtS >
 => < mtp : MTP | state: load-grammar, AtS >
    insertModule(MI, mtp, GRAMMAR) .

 rl [load-grammar-finished] :
    insertedModule(mtp, MI)
    < mtp : MTP | state: load-grammar, AtS >
 => < mtp : MTP | state: load-comm-grammar, AtS >
    insertModule(MI, mtp, COMM-GRAMMAR) .

 rl [load-comm-grammar-finished] :
    insertedModule(mtp, MI)
    < mtp : MTP | state: load-comm-grammar, AtS >
 => < mtp : MTP | state: load-user-mi, AtS >
    createInterpreter(interpreterManager, mtp, none) .

 rl [user-metainterpreter-created] :
    createdInterpreter(mtp, interpreterManager, MI)
    < mtp : MTP | state: load-user-mi, uload: module(Q) LP, AtS >
 => < mtp : MTP | state: load-user-mods, umi: MI, uload: LP, AtS >
    insertModule(MI, mtp, upModule(Q, false)) .

 rl [load-user-predef-module-metainterpreter] :
    insertedModule(mtp, MI)
    < mtp : MTP | state: load-user-mods, uload: module(Q) LP, AtS >
 => < mtp : MTP | state: load-user-mods, uload: LP, AtS >
    insertModule(MI, mtp, upModule(Q, false)) .

 rl [user-predef-module-metainterpreter-finished] :
    insertedModule(mtp, MI)
    < mtp : MTP | state: load-user-mods, uload: nil, AtS >
 => < mtp : MTP | state: idle, AtS >
    getLine(stdin, mtp, "MTP> ") .

 ***                        ***
 ***          RUN           ***
 ***                        ***

 eq run =
    <> < mtp : MTP | file: null, out: "", state: init, db: empty,
                     load: modList, uload: umodList, current: noQid,
                     parsModName: noQid, bubbles: empty, top: noMod,
                     parsedModules: empty, input: empty, module: noMod,
                     goal: noGoal >
    createInterpreter(interpreterManager, mtp, none) .
endm

set print conceal on .
print conceal module:_ .

set print attribute on .
set show advisories on .

erew run .

*** goal MyLogic |- eq forall L . empty = empty
